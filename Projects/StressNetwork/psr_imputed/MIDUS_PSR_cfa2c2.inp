DATA:
FILE = "MIDUS_PSR_cfa2c2.dat";
 
VARIABLE:
NAMES = M2ID M2FAMNUM M1 M2 M3 M4 LC1 LC2 LC3 LC4 LC5 LC6 LC7 LC8 SE1 SE2 SE3 SE4
     SE5 SE6 SE7 Opt1 Opt2 Opt3 Pess1 Pess2 Pess3 PLife1 PLife2 PLife3 PLife4 PLife5
     PLife6 PLife7 Ctrl1 Ctrl2 Ctrl3 Con1 Con2 Con3 Con4 Con5 Ext1 Ext2 Ext3 Ext4
     Ext5 FamS1 FamS2 FamS3 FamS4 FriS1 FriS2 FriS3 FriS4 SpoS1 SpoS2 SpoS3 SpoS4
     SpoS5 SpoS6 SI1 SI2 SI3 AGE SEX RACE; 
 MISSING=.;
 
USEVARIABLES ARE
 M1 M2 M3  LC2 LC5 LC7
 SE2 SE4 SE6  Opt1 Opt2 Opt3
 PLife2 PLife3 PLife5
 Con2 Con3 Con5  Ext1 Ext3 Ext4
 FamS1 FamS2 FamS4  FriS1 FriS2 FriS4
 SpoS1 SpoS2 SpoS3  SI1 SI2 SI3;

CATEGORICAL ARE
 M1 M2 M3  LC2 LC5 LC7
 SE2 SE4 SE6  Opt1 Opt2 Opt3
 PLife2 PLife3 PLife5
 Con2 Con3 Con5  Ext1 Ext3 Ext4
 FamS1 FamS2 FamS4  FriS1 FriS2 FriS4
 SpoS1 SpoS2 SpoS3  SI1 SI2 SI3;

IDVARIABLE = M2ID;

ANALYSIS:

  ESTIMATOR = WLSMV;
  PROCESSORS = 2;
MODEL:

 M BY M1 M2 M3;
 LC BY LC2@-1 LC5*-1 LC7*-1;
 SE BY SE2@1 SE4*1 SE6*1;
 Opt BY Opt1 Opt2 Opt3;
 PLife BY PLife2@1 PLife3 PLife5;
 Con BY Con2@1 Con3 Con5;
 EXT BY Ext1@1 Ext3 Ext4;
 FamS BY FamS1 FamS2 FamS4;
 FriS BY FriS1 FriS2 FriS4;
 SpoS BY SpoS1 SpoS2 SpoS3;
 SI BY SI1@-1 SI2*1 SI3*1;

 PR@1;
 PR BY M* LC SE Opt !Pess
   PLife Con EXT;
 SR@1;
 SR BY FamS* FriS SpoS SI;
 PR WITH SR*;

OUTPUT:
STDYX; CINTERVAL;
SAVEDATA:

  FILE = MIDUS_PSR_cfa2c2_fscores.dat;
  SAVE = FSCORES;
PLOT:
TYPE IS PLOT2;
